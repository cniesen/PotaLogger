import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.openjfx.javafxplugin' version '0.0.9'
	id 'org.beryx.runtime' version '1.11.4'
}

group = 'com.niesens'
version = '1.1'
sourceCompatibility = '14'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.openjfx:javafx-controls:14'
	implementation 'org.openjfx:javafx-fxml:14'
	implementation 'org.openjfx:javafx-graphics:14:win'
	implementation 'org.openjfx:javafx-graphics:14:mac'
	implementation 'org.openjfx:javafx-graphics:14:linux'
	implementation 'commons-io:commons-io:2.7'
	implementation 'org.apache.commons:commons-lang3:3.11'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
	useJUnitPlatform()
}


javafx {
	modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application {
	mainClassName = 'com.niesens.potalogger.Main'
	applicationName = 'PotaLogger'
}

runtime {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	launcher {
		noConsole = true
	}
	jpackage {
		def currentOs = org.gradle.internal.os.OperatingSystem.current()
		def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
		imageOptions += ['--icon', "src/main/resources/PotaLogger.$imgType"]
		installerOptions += ['--resource-dir', "build/jpackage/LotaLogger/lib"]
		installerOptions += ['--app-version', version]
		installerOptions += ['--copyright', "Copyright 2020 Claus Niesen"]
		installerOptions += ['--name', "Claus' POTA Logger"]
		installerOptions += ['--vendor', "Claus Niesen"]
		installerOptions += ['--license-file', "COPYING.md"]
		if(currentOs.windows) {
			installerOptions += ['--win-dir-chooser', '--win-menu', '--win-shortcut']
		}
	}
}

processResources {
	filesMatching('*.properties') {
		filter ReplaceTokens, tokens: [
				'application.version': version
		]
	}
}